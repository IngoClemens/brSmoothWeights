// ---------------------------------------------------------------------
// brSmoothWeightsCreateMenuItems.mel
//
// Creates menu items for the smooth weights tool and the transfer
// weights tool in the default Maya skin menu.
//
// ---------------------------------------------------------------------

global proc brSmoothWeightsDeleteMenuItems()
{
    deleteUI brSmoothWeightsMenuDivider;
    deleteUI brSmoothWeightsToolMenuItem;
    deleteUI brSmoothWeightsToolOptionMenuItem;
    deleteUI brTransferWeightsToolMenuItem;
    deleteUI brTransferWeightsToolOptionMenuItem;
}


global proc brSmoothWeightsAddMenuCommand()
{
    global string $gRigSkinningMenu;

    if (`menu -exists $gRigSkinningMenu`)
    {
        string $cmd = `menu -query -postMenuCommand $gRigSkinningMenu`;
        $cmd += "; brSmoothWeightsCreateMenuItems";
        menu -edit -postMenuCommand $cmd $gRigSkinningMenu;
    }

    source dagMenuProc;
}


proc addMenuItems()
{
    string $ann = "Paint smooth skin cluster weights for the selection";

    menuItem -label "Paint Smooth Weights Tool"
             -annotation $ann
             -image "brSmoothWeights.svg"
             -command "brSmoothWeightsToolCtx"
             brSmoothWeightsToolMenuItem;
    menuItem -optionBox 1
             -annotation $ann
             -image "brSmoothWeights.svg"
             -command "brSmoothWeightsToolCtx; toolPropertyWindow;"
             brSmoothWeightsToolOptionMenuItem;

    $ann = "Transfer skin cluster weights from one influence to another.";

    menuItem -label "Paint Transfer Weights Tool"
             -annotation $ann
             -image "brTransferWeights.svg"
             -command "brTransferWeightsToolCtx"
             brTransferWeightsToolMenuItem;
    menuItem -optionBox 1
             -annotation $ann
             -image "brTransferWeights.svg"
             -command "brTransferWeightsToolCtx; toolPropertyWindow;"
             brTransferWeightsToolOptionMenuItem;
}


global proc brSmoothWeightsCreateMenuItems()
{
    global string $gRigSkinningMenu;

    if (`menuItem -query -exists brSmoothWeightsMenuDivider`)
        return;

    if (!`pluginInfo -query -loaded brSmoothWeights`)
        loadPlugin brSmoothWeights;

    if (!`menu -query -numberOfItems $gRigSkinningMenu`)
        ChaSkinningMenu $gRigSkinningMenu;

    setParent -menu $gRigSkinningMenu;
    string $div = "-dividerLabel \"Smooth Skin Weights\" ";
    eval("menuItem -divider 1 " + $div + "brSmoothWeightsMenuDivider");

    addMenuItems;
}

// ---------------------------------------------------------------------
// MIT License
//
// Copyright (c) 2021 Ingo Clemens, brave rabbit
// brSmoothWeights and brTransferWeights are under the terms of the MIT
// License
//
// Permission is hereby granted, free of charge, to any person obtaining
// a copy of this software and associated documentation files (the
// "Software"), to deal in the Software without restriction, including
// without limitation the rights to use, copy, modify, merge, publish,
// distribute, sublicense, and/or sell copies of the Software, and to
// permit persons to whom the Software is furnished to do so, subject to
// the following conditions:
//
// The above copyright notice and this permission notice shall be
// included in all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
// EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
// MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
// IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
// CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
// TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
// SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
//
// Author: Ingo Clemens    www.braverabbit.com
// ---------------------------------------------------------------------
